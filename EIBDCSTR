//EIBDCSTR JOB MISEIS,EIBDCSTR,CLASS=A,MSGCLASS=X,NOTIFY=&SYSUID,
//         USER=OPCC
/*JOBPARM S=S1M2
//*
//EIBWUNIS EXEC SAS609,REGION=6M,WORK='120000,8000'
//RAW      DD DSN=SAP.CRM.UNICARD.A33(0),DISP=SHR
//         DD DSN=SAP.CRM.UNICARD.A55(0),DISP=SHR
//CRM      DD DSN=SAP.PBB.CRM.UNITRSAC,DISP=OLD
//SASLIST  DD SYSOUT=X
//SYSIN    DD *
OPTIONS YEARCUTOFF=1950 NOCENTER NODATE NONUMBER MISSING=0;
*------------------------------------------------*
*  GET REPTDATE                                  *
*------------------------------------------------*;
DATA REPTDATE;
  INFILE RAW OBS=1;
  INPUT @019 INPUTDT $6.;
  REPTDATE=INPUT(INPUTDT,DDMMYY6.);
  SELECT;
     WHEN(1<= DAY(REPTDATE)<= 8) DO; WK = '01'; END;
     WHEN(9<= DAY(REPTDATE)<= 15) DO; WK = '02'; END;
     WHEN(16<= DAY(REPTDATE)<= 22) DO; WK = '03'; END;
     OTHERWISE DO; WK = '04'; END;
  END;
  MM = MONTH(REPTDATE);
  IF WK = '01' THEN DO;
     MM1 = MM - 1;
     IF MM1 = 0 THEN MM1 = 12;
  END;
  ELSE MM1 = MM;
  WEEK = WEEKDAY(REPTDATE) - 1;
  IF DAY(REPTDATE)- 1 IN (1,9,16,22) AND
     WEEK - 1  = 0 THEN WEEKIND='Y';
  CALL SYMPUT('NOWK',PUT(WK,$2.));
  CALL SYMPUT('REPTDT', REPTDATE);
  CALL SYMPUT('REPTDAY', PUT(DAY(REPTDATE), Z2.));
  CALL SYMPUT('REPTMON', PUT(MM, Z2.));
  CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR2.));
  CALL SYMPUT('WEEKIND',PUT(WEEKIND,$1.));
RUN;
*;
DATA CARD (KEEP=ACCTNO CARDHOLD ISSNBR CHKDIGIT PURCHDAT
                TRANCODE MERCHANC TRANAMT TIME REPTDATE);
  INFILE RAW;
  INPUT @01 ACCTNO    11.
        @12 CARDHOLD   1.
        @13 ISSNBR     1.
        @14 CHKDIGIT   1.
        @15 PURCHDAT  $4.
        @19 INPUTDT   $6.
        @25 TRANCODE   4.
        @29 MERCHANC   4.
        @33 TRANAMT    9.2
        @42 SIGN      $1.
        @43 TIME       8.;
  REPTDATE=INPUT(INPUTDT,DDMMYY6.);
  IF SIGN = '-' THEN TRANAMT = (-1)*TRANAMT;
*;
PROC SORT DATA=CARD;
   BY ACCTNO;
*;
PROC PRINT DATA=CARD (OBS=100); RUN;
PROC PRINT DATA=REPTDATE;
FORMAT REPTDATE DDMMYY8.; RUN;
%MACRO APPEND;
%IF "&REPTDAY" EQ "01" OR
    "&REPTDAY" EQ "09" OR
    "&REPTDAY" EQ "16" OR
    "&REPTDAY" EQ "23" OR
    "&WEEKIND" EQ "Y"  OR
    %SYSFUNC(EXIST(CRM.UNITRAN&REPTYEAR&REPTMON&NOWK)) = 0 %THEN
    %DO;
      PROC DATASETS LIB=CRM NOLIST;
        DELETE UNITRAN&REPTYEAR&REPTMON&NOWK;
      RUN;
      PROC APPEND DATA=CARD BASE=CRM.UNITRAN&REPTYEAR&REPTMON&NOWK;
      RUN;
    %END;
%ELSE %DO;
      DATA CRM.UNITRAN&REPTYEAR&REPTMON&NOWK;
        SET CRM.UNITRAN&REPTYEAR&REPTMON&NOWK;
        IF REPTDATE EQ "&REPTDT" THEN DELETE;
      RUN;
      PROC APPEND DATA=CARD
                  BASE=CRM.UNITRAN&REPTYEAR&REPTMON&NOWK;RUN;
%END;
%MEND APPEND;
%APPEND;
/*
