//EIIMBTAT JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=8M                      JOB33290
/*JOBPARM S=S1M2
//*----------------------------------------------------------------
//* PROGRAM  : EIIMBTAT
//* DESC     : EXTRACT AVERAGE ORIGINAL TENURE FOR BAS PRIMARY
//*            REDISCOUNTED
//* DATASET  : IBTRADMMWYY
//* ESMR     : 2011-3979 (AAB)
//*----------------------------------------------------------------
//DELETE  EXEC PGM=IEFBR14
//DEL01   DD DSN=SAP.EIIMBTAT.TEXT,
//           DISP=(MOD,DELETE,DELETE),
//           SPACE=(CYL,(0))
//*----------------------------------------------------------------
//EIIMBTAT  EXEC SAS609
//LOAN      DD DSN=SAP.PIBB.MNILN(0),DISP=SHR
//BTRWH     DD DSN=SAP.PIBB.BTDATAWH,DISP=SHR
//PGM       DD DSN=SAP.BNM.PROGRAM,DISP=SHR
//SASLIST   DD DSN=SAP.EIIMBTAT.TEXT,
//             DISP=(NEW,CATLG,DELETE),
//             SPACE=(CYL,(3,2),RLSE),UNIT=SYSDA,
//             DCB=(LRECL=133,RECFM=FB,BLKSIZE=0)
//SYSIN     DD *

OPTIONS YEARCUTOFF=1960 NOCENTER NODATE NONUMBER MISSING=0;

DATA REPTDATE;
   SET LOAN.REPTDATE;
   SELECT(DAY(REPTDATE));
      WHEN(8)   CALL SYMPUT('NOWK', PUT('1', $1.));
      WHEN(15)  CALL SYMPUT('NOWK', PUT('2', $1.));
      WHEN(22)  CALL SYMPUT('NOWK', PUT('3', $1.));
      OTHERWISE CALL SYMPUT('NOWK', PUT('4', $1.));
   END;
   CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR2.));
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY10.));
RUN;

DATA IBTRAD;
   SET BTRWH.IBTRAD&REPTMON&NOWK&REPTYEAR;
   WHERE FACILITY IN ('34411','34412','34421','34422','34440','34470'
                      '34470','34480','34490') AND
         LIABCODE IN ('BPI','BII','BSI','BEI') AND
         UTRDF EQ 'D' AND BALANCE GT 0;
   KEEP BRANCH ACCTNOX TRANSREF FCVALUE MATDATE ISSDTE TENURE TOTAMT;
   FORMAT TENURE   12.;
   TENURE = (MATDATE-ISSDTE)+1;
   TOTAMT = FCVALUE*TENURE;
RUN;

PROC SORT DATA=IBTRAD; BY BRANCH; RUN;

PROC SUMMARY DATA=IBTRAD NWAY;
   BY BRANCH;
   VAR FCVALUE TOTAMT;
   OUTPUT OUT=AVGTENURE(DROP=_FREQ_ _TYPE_) SUM=;
RUN;

DATA AVGTENURE;
   SET AVGTENURE;
   TENURE = TOTAMT/FCVALUE;
RUN;

PROC REPORT DATA=AVGTENURE NOWD HEADLINE SPLIT='*' PS=2000;
   TITLE1 ' P U B L I C   I S L A M I C   B A N K   B E R H A D';
   TITLE2 ' MANAGEMENT ACCOUNTING, FINANCE DIVISION';
   TITLE4 ' REPORT ID    : EIIMBTAT';
   TITLE5 ' REPORT TITLE : AVERAGE ORIGINAL TENURE FOR BA PRIMARY '
                           'REDISCOUNTED';
   TITLE6 ' REPORT DATE  : ' "&RDATE";
   COLUMN BRANCH TOTAMT FCVALUE TENURE AVGTEN;
   DEFINE BRANCH   / DISPLAY     FORMAT=8.   'BRANCH'
                     WIDTH=10;
   DEFINE TOTAMT   / SUM     FORMAT=COMMA22. 'TOTAL AMOUNT (RM)'
                     WIDTH=30;
   DEFINE FCVALUE  / SUM     FORMAT=COMMA20. 'BALANCE (RM)'
                     WIDTH=20;
   DEFINE TENURE   / DISPLAY     FORMAT=8.   'AVG TENURE (DAY)'
                     WIDTH=20;
   DEFINE AVGTEN   / COMPUTED NOPRINT;

   COMPUTE AVGTEN;
      AVGTEN=TOTAMT.SUM/FCVALUE.SUM;
   ENDCOMP;
   RBREAK AFTER  /SKIP;
   COMPUTE AFTER;
       LINE @003 86*'-';
       LINE @015 'TOTAL :'
            @023 TOTAMT.SUM   COMMA22.
            @047 FCVALUE.SUM  COMMA20.
            @073 'AVG :'
            @080 AVGTEN          9.;
       LINE @003 86*'=';
   ENDCOMP;
RUN;
